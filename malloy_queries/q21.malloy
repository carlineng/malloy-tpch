import "tpch.malloy"

// This query is implemented a bit differently from the SQL
// due to lack of `exists` gesture in Malloy

run: lineitem extend {
  join_many: l2 is lineitem -> {
    select:
      l_orderkey
      l_suppkey
      l_receiptdate
      l_commitdate
  } on l_orderkey = l2.l_orderkey
} -> {
  group_by: 
    supplier.s_name
    l_orderkey

  aggregate:
    l2count is l2.count() { where: l_suppkey != l2.l_suppkey }
    l3count is l2.count() { where: l_suppkey != l2.l_suppkey and l2.l_receiptdate > l2.l_commitdate }

  where:
    orders.o_orderstatus = 'F'
    and l_receiptdate > l_commitdate
    and supplier.nation.n_name = 'SAUDI ARABIA'
} -> {
  group_by: s_name
  aggregate: numwait is count()
  where: l2count > 0 and l3count = 0
  order_by: numwait desc, s_name
  limit: 100
}
